buildscript {
  ext {
    springBootVersion="2.1.5.RELEASE"
    springCloudVersion="Greenwich.SR1"
    gradlePath="${rootProject.projectDir}/gradle"
    groovyVersion="2.5.7"
    jibVersion="1.2.0"
    lombokPluginVersion = "3.6.4"
    gitPluginVersion = "0.12.0-rc2"
    coverallsPluginVersion = "2.8.4"
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:${jibVersion}"
    classpath "io.freefair.gradle:lombok-plugin:${lombokPluginVersion}"
    classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:${gitPluginVersion}"
    classpath "gradle.plugin.com.github.kt3k.coveralls:coveralls-gradle-plugin:${coverallsPluginVersion}"
  }
}

allprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: "java"
  apply plugin: "groovy"
  apply plugin: "org.springframework.boot"
  apply plugin: "io.spring.dependency-management"
  apply plugin: "io.freefair.lombok"
  apply plugin: "com.palantir.git-version"

  sourceCompatibility = '11'
  group = 'clustercamp'

  configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
  }

  dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation("org.springframework.boot:spring-boot-starter-undertow")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.codehaus.groovy:groovy-all:${groovyVersion}")

    testImplementation("io.projectreactor:reactor-test")
    testImplementation("org.springframework.boot:spring-boot-starter-test")

    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
  }
}

apply from: "${gradlePath}/api.gradle"
apply from: "${gradlePath}/jib.gradle"
apply from: "${gradlePath}/checkstyle.gradle"

