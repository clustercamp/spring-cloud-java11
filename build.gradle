buildscript {
  ext {
    springBootVersion = "2.1.6.RELEASE"
    springCloudVersion = "Greenwich.SR1"
    gradlePath = "${rootProject.projectDir}/gradle"
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:${jibVersion}"
    classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:${gitPluginVersion}"
    classpath "io.freefair.gradle:lombok-plugin:${lombokPluginVersion}"
  }
}

allprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: "java"
  apply plugin: "groovy"
  apply plugin: "org.springframework.boot"
  apply plugin: "io.spring.dependency-management"
  apply plugin: "io.freefair.lombok"

  sourceCompatibility = '11'
  group = 'clustercamp'

  configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
  }

  dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation("org.springframework.boot:spring-boot-starter-undertow")
    implementation("org.springframework.boot:spring-boot-starter-actuator")

    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("io.projectreactor:reactor-test")
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
  }
}

apply from: "${gradlePath}/api.gradle"
apply from: "${gradlePath}/jib.gradle"
apply from: "${gradlePath}/metric.gradle"
